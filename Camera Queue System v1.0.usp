/*******************************************************************************************
  SIMPL+ Module Information
  (Fill in comments below)
*******************************************************************************************/
/*
Dealer Name: Crestron Singapore Pte Ltd
System Name: Camera Queue System v1.0
System Number: v1.0
Programmer: Eric Low
Comments:
*/

/*******************************************************************************************
  Compiler Directives
  (Uncomment and declare compiler directives as needed)
*******************************************************************************************/
#SYMBOL_NAME "Camera Queue System v1.0"
//System Constant
#DEFINE_CONSTANT FALSE 0
#DEFINE_CONSTANT TRUE 1
#DEFINE_CONSTANT NULL 0

//Camera Constant
#DEFINE_CONSTANT ciMAX_CAM 36

#CATEGORY "19" // Camera
#DEFAULT_VOLATILE
#ENABLE_STACK_CHECKING
#ENABLE_TRACE

/*******************************************************************************************
  DIGITAL, ANALOG and SERIAL INPUTS and OUTPUTS
  (Uncomment and declare inputs and outputs as needed)
*******************************************************************************************/
DIGITAL_INPUT _SKIP_, Mic_On[ciMAX_CAM], _SKIP_, Mic_Off[ciMAX_CAM, ciMAX_CAM];

ANALOG_OUTPUT _SKIP_, Preset;

/*******************************************************************************************
  Parameters
  (Uncomment and declare parameters as needed)
*******************************************************************************************/
INTEGER_PARAMETER Default_Preset;

/*******************************************************************************************
  Parameter Properties
  (Uncomment and declare parameter properties as needed)
*******************************************************************************************/
#BEGIN_PARAMETER_PROPERTIES Default_Preset
	propValidUnits = unitDecimal;
	propDefaultUnit = unitDecimal;
	propDefaultValue = 37d;
#END_PARAMETER_PROPERTIES

/*******************************************************************************************
  Global Variables
  (Uncomment and declare global variables as needed)
  Note:  Be sure to initialize all declared STRING variables as needed
         For example, in Function Main: myString = "";
*******************************************************************************************/
INTEGER giPreset, giQueue[ciMAX_CAM], giQueueCounter;

/*******************************************************************************************
  Functions
  (Add any additional functions here)
  Note:  Functions must be physically placed before the location in
         the code that calls them.
*******************************************************************************************/
Function Initialize()
{
	Integer i;
	
	giQueueCounter = NULL;
	giPreset = Default_Preset;	
	
	For(i = 1 To ciMAX_CAM)
		giQueue[i] = NULL;		
}

Function AddQueue(Integer Index)
{
	giQueueCounter = giQueueCounter + 1;
	giQueue[giQueueCounter] = Index;
	
	giPreset = giQueue[giQueueCounter];	
	Preset = giPreset;
	
	//Trace("Add Queue %d: Camera Position is --> %d\n", giQueueCounter, giQueue[giQueueCounter]);
}

Function RemoveQueue(Integer Index)
{
	Integer i, j;
	
	For(i = 1 To giQueueCounter)
	{
		If(giQueue[i] = Index)
		{
			//Trace("Camera Position %d Found! Removing Queue %d: Camera Position is --> %d\n", Index, i, giQueue[i]);			
			giQueue[i] = NULL;
			
			If(i < giQueueCounter)
			{
				For(j = i To giQueueCounter - 1)				
					giQueue[j] = giQueue[j + 1];
			}
				
			giQueueCounter = giQueueCounter - 1;
			
			Break;
		}
	}
	
	//Set Camera Preset
	If(giQueueCounter <> NULL)
		giPreset = giQueue[giQueueCounter];
	Else
		giPreset = Default_Preset;
	
	Preset = giPreset;
	
	//Trace("Set Camera Position %d from Queue %d\n", giPreset, giQueueCounter);
}

/*******************************************************************************************
  Event Handlers
  (Uncomment and declare additional event handlers as needed)
*******************************************************************************************/
THREADSAFE PUSH Mic_On
{
	If(giQueueCounter < ciMAX_CAM)
		AddQueue(GetLastModifiedArrayIndex());
}

THREADSAFE PUSH Mic_Off
{
	If(giQueueCounter > NULL)
		RemoveQueue(GetLastModifiedArrayIndex());
}

/*******************************************************************************************
  Main()
  Uncomment and place one-time startup code here
  (This code will get called when the system starts up)
*******************************************************************************************/
Function Main()
{
	WaitForInitializationComplete();
	Initialize();
	Preset = giPreset;    
}
